#! /usr/bin/perl -w

=head1 NAME

dh_click - install system hooks for click

=cut

use strict;
use Debian::Debhelper::Dh_Lib;

=head1 SYNOPSIS

B<dh_click> [S<B<debhelper options>>]

=head1 DESCRIPTION

dh_click is a debhelper program that is responsible for installing system
hooks for B<click>.

It also automatically generates the F<postinst> and F<postrm> commands
needed to interface with the Ubuntu B<click> package.  These commands are
inserted into the maintainer scripts by L<dh_installdeb(1)>.

=head1 FILES

=over 4

=item debian/I<package>.click-hook

Click package hook files, installed into
usr/share/click/hooks/I<package>.hook in the package build directory.  See
F</usr/share/doc/click-doc/html/hooks.html> for their format.

=back

=head1 OPTIONS

=over 4

=item B<-n>, B<--noscripts>

Do not modify F<postinst>/F<postrm> scripts.

=item B<--name=>I<name>

Install the hook using the filename I<name> instead of the default filename,
which is the package name. When this parameter is used, B<dh_click> looks
for and installs files named F<debian/package.name.hook>, instead of the
usual F<debian/package.hook>.

=back

=head1 EXAMPLES

dh_click is usually called indirectly in a rules file via the dh command.

	%:
		dh $@ --with click

You must build-depend on at least debhelper (>= 7.0.8) to use this form, and
in any case you must build-depend on click-dev to use this program at all.

It can also be called directly at any time before C<dh_installdeb>, usually
in a binary-arch or binary-indep rule.

=cut

init();

# PROMISE: DH NOOP WITHOUT click-hook

foreach my $package (@{$dh{DOPACKAGES}}) {
	my $tmp=tmpdir($package);
	my $click_hook=pkgfile($package,"click-hook");
	my $hookname=$package;
	if (defined $dh{NAME}) {
		$hookname=$dh{NAME};
	}

	if ($click_hook ne '') {
		if (! -d "$tmp/usr/share/click/hooks") {
			doit("install","-d","$tmp/usr/share/click/hooks");
		}
		doit("install","-p","-m644",$click_hook,"$tmp/usr/share/click/hooks/$hookname.hook");

		if (! $dh{NOSCRIPTS}) {
			autoscript($package,"postinst","postinst-click","s/#HOOK#/$hookname/");
			autoscript($package,"prerm","prerm-click","s/#HOOK#/$hookname/");
		}
	}
}

=head1 SEE ALSO

L<debhelper(7)>

This program is a part of click.

=head1 AUTHOR

Colin Watson <cjwatson@ubuntu.com>

Copyright (C) 2013 Canonical Ltd., licensed under the GNU GPL v3.

=cut
